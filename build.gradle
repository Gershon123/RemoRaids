buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            url "http://repository.jboss.org/nexus/content/groups/public/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
    }
}

plugins {
    id 'java'
    id 'maven'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'ninja.miserable.blossom' version '1.0.1'
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: 'maven-publish'
apply plugin: 'maven-publish-auth'

version = modVersion
group = modGroup
archivesBaseName = modBaseName

ext {
    pixelmon = "7.2.0"
}

minecraft {
    version = project.forgeVersion
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = project.mcpVersion
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
    mavenCentral()
    ivy {
        url "https://download.nodecdn.net/containers"
        layout "pattern", {
            artifact "/[organisation]/[module]/release/[revision]/[ext]"
        }
    }
}

dependencies {
    compile 'com.google.guava:guava:27.1-jre'
    shadow 'reforged:server:7.2.2@Pixelmon-1.12.2-7.2.2-server.jar'
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

shadowJar {
    archiveName = "RemoRaids-${project.ext.pixelmon}-${version}.jar"

    relocate 'org.checkerframework', 'ca.landonjw.remoraids.internal.relocations.checkerframework'

    exclude 'dummyThing'
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}

build.dependsOn reobfShadowJar

publishing {
    repositories {
        maven {
            name = "ImpactDev-Public"
            url = "https://maven.impactdev.net/repository/development/"
            credentials {
                username System.getenv('NEXUS_USER')
                password System.getenv('NEXUS_PW')
            }
        }
    }

    publications {
        remoraids(MavenPublication) {
            from components.java
        }
    }
}